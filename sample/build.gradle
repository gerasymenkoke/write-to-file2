apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs {
        def debugKeystore = file("${System.properties['user.home']}${File.separator}.android${File.separator}debug.keystore")
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile debugKeystore
        }
        release {
            if (properties['keyAlias'] != null) {
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
                storePassword properties['storePassword']
                storeFile file('keystore.jks')
            } else {
                keyAlias 'androiddebugkey'
                keyPassword 'android'
                storePassword 'android'
                storeFile debugKeystore
            }
        }
    }

    defaultConfig {
        applicationId "com.anggrayudi.storage.sample"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding = true
    }

    flavorDimensions "libSource"
    productFlavors {
        local {
            dimension "libSource"
            getIsDefault().set(true)
        }
        maven {
            dimension "libSource"
            configurations.all {
                // Check for updates every build
                resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":storage")
//    localImplementation project(":storage")
//    mavenImplementation("$GROUP:$POM_ARTIFACT_ID:$VERSION_NAME") { changing = true }

    implementation deps.core_ktx
    implementation deps.appcompat
    implementation deps.multidex

    implementation deps.timber
    implementation deps.material_progressbar
    implementation 'androidx.preference:preference-ktx:1.2.0'

    //test
    testImplementation deps.junit
    testImplementation deps.mockk
}